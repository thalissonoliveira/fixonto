//####### EXTENSÃO DA FIXTURE COM FEA2ONTO 2.O ##########

//####### WIZARD 1. FEA2ONTO 2.0 #########
wizard FeaToOntoFixtureParser
{
	guard: self.isTypeOf(LPS)
	title: 'Fixture - Fea2Onto 2.0'
	do {
		fixTure2(self);	
	}
}

//####### WIZARD 2. ONTO ANALYSER #########
wizard OntoAnalyser
{
	guard: self.isTypeOf(LPS)
	title: 'MMSC Analyser Fea2Onto 2.0'
	do {
		ontoAnalyserFixture();
	}
}


operation fixTure2(lps:LPS) {
	//#### Nessa etapa será montada a estrutura da LPS para que seja possível traduzir em ontologia ########\\
	var featToOntoFixture := new Native('fixture.owl.parser.FeaToOntoFixture');
	var factoryFixture := featToOntoFixture.getSplConceptFactory();
	var splFixture = factoryFixture.createSpl(lps.id, lps.nome);
	
	splFixture = construirObjetoSPL(lps, factoryFixture, splFixture, featToOntoFixture);
	featToOntoFixture.run(splFixture);
}


operation construirObjetoSPL(lps:LPS, factoryFixture, splFixture, featToOntoFixture) {
	
	var raiz = lps.sistema;
 	var rootFixture = factoryFixture.createRootFeature(raiz.id, raiz.nome);
 	
 	featToOntoFixture.addToFixtureOracle(splFixture);
	featToOntoFixture.addToFixtureOracle(rootFixture);
	
	splFixture.setSystem(rootFixture);
	
	var elementFixture;
	for (elemento in lps.elementos) {
		if (elemento.isTypeOf(CaracteristicaRaiz)) {
			continue;
		}
		elementFixture = criarElementoFixture(elemento, factoryFixture);
		splFixture.getElements().add(elementFixture);
		featToOntoFixture.addToFixtureOracle(elementFixture);
	}
	
	var ruleFixture;
	for (regra in lps.regras) {
		ruleFixture = criarRegraFixture(regra, factoryFixture);
		splFixture.getRules().add(ruleFixture);
		featToOntoFixture.addToFixtureOracle(ruleFixture);
	}
	
	var expressionFixture;
	for (expressao in lps.expressoes) {
		expressionFixture = criarExpressaoFixture(expressao, factoryFixture);
		featToOntoFixture.addToFixtureOracle(expressionFixture);
	}
	
	preencherElementosCaracteristicaLpsFixture(raiz, factoryFixture, splFixture, featToOntoFixture);
	preencherElementosContextuaisLpsFixture(lps, factoryFixture, splFixture, featToOntoFixture);
	preencherRegrasLpsFixture(lps, factoryFixture, splFixture, featToOntoFixture);
		
	return splFixture;
}

operation preencherRegrasLpsFixture(lps : LPS, factoryFixture, splFixture, featToOntoFixture) {
	var ruleFixture = null;

	for (regra in lps.regras) {
		if (regra.isTypeOf(RegraDeComposicao)) {
			ruleFixture = preencherRegraDeComposicao(regra, factoryFixture, splFixture, featToOntoFixture);
		} else if (regra.isTypeOf(RegraDeContexto)) {
			ruleFixture = preencherRegraDeContexto(regra, factoryFixture, splFixture, featToOntoFixture);
		} else {
			'[ERROR] - Erro ao criar nova Regra pela Fixture: regra do modelo é inválida.'.println();
			return;
		}
		
		splFixture.getRules.add(ruleFixture);
	}
}


operation preencherElementosCaracteristicaLpsFixture(caracteristicaRaiz : Caracteristica, factoryFixture, splFixture, featToOntoFixture) {
		
	var featureFixture = featToOntoFixture.getOracle().get(caracteristicaRaiz.id);
	var childFeatureFixture = null;
	var attributeFixture = null;
	
	for (atributo in caracteristicaRaiz.atributo) {
		attributeFixture = featToOntoFixture.getOracle().get(atributo.id);
		featureFixture.addAttribute(attributeFixture);
	}
	
	for (caracteristicaFilha in caracteristicaRaiz.caracteristicaFilha) {
		childFeatureFixture = featToOntoFixture.getOracle().get(caracteristicaFilha.id);
		featureFixture.addChild(childFeatureFixture);
		
		for (atributo in caracteristicaFilha.atributo) {
			attributeFixture = featToOntoFixture.getOracle().get(atributo.id);
			featureFixture.addAttribute(attributeFixture);
		}
		
		if (not caracteristicaFilha.caracteristicaFilha.isEmpty()) {
			splFixture = preencherElementosCaracteristicaLpsFixture(caracteristicaFilha, factoryFixture, splFixture, featToOntoFixture);
		}
		
	}
	return splFixture;
}


operation preencherElementosContextuaisLpsFixture(lps:LPS, factoryFixture, splFixture, featToOntoFixture) {
	var contextRootFixture = null;
	var contextEntityFixture = null;
	var contextInfoFixture = null;

	for (raizDeContexto in lps.elementos -> select(x|x.isTypeOf(RaizDeContexto))) {
		contextRootFixture = featToOntoFixture.getOracle().get(raizDeContexto.id);
		
		for (entidadeDeContexto in raizDeContexto.entidadesDeContexto) {
			contextEntityFixture = featToOntoFixture.getOracle().get(entidadeDeContexto.id);
			contextEntityFixture.setFatherContextRoot(contextRootFixture);
			contextRootFixture.getContextEntities().add(contextEntityFixture);
		
			for (informacaoDeContexto in entidadeDeContexto.informacoesdeContexto) {
				contextInfoFixture = featToOntoFixture.getOracle().get(informacaoDeContexto.id);
				contextInfoFixture.setFatherContextEntity(contextEntityFixture);
				contextEntityFixture.getContextInfos().add(contextInfoFixture);
			}
		}
	}
}


operation preencherRegraDeContexto(regraDeContexto: RegraDeContexto, factoryFixture, splFixture, featToOntoFixture) {
	var contextRuleFixture = null;
	var eventFixture = null;
	var actionFixture = null;
	
	contextRuleFixture = featToOntoFixture.getOracle().get(regraDeContexto.id);
	eventFixture = preencherEvento(regraDeContexto.evento, factoryFixture, splFixture, featToOntoFixture);
	actionFixture = preencherAcao(regraDeContexto.acao, factoryFixture, splFixture, featToOntoFixture);
	contextRuleFixture.setEvent(eventFixture);
	contextRuleFixture.setAction(actionFixture);
	
	return contextRuleFixture;
	
}

operation preencherAcao(acao: Acao, factoryFixture, splFixture, featToOntoFixture) {
	var actionFixture = null;
	if (acao.isTypeOf(AcaoLogico)) {
		actionFixture = featToOntoFixture.getOracle().get(acao.id);
		actionFixture.setLeftSideAction(preencherAcao(acao.ladoEsquerdoAcao, factoryFixture, splFixture, featToOntoFixture));
		actionFixture.setRightSideAction(preencherAcao(acao.ladoDireitoAcao, factoryFixture, splFixture, featToOntoFixture));
		actionFixture.setLogicalActionOperator(acao.operadorAcaoLogico.value);
		return actionFixture;
	} else if (acao.isTypeOf(LiteralAcao)) {
		actionFixture = featToOntoFixture.getOracle().get(acao.id);
		actionFixture.setPresence(acao.presenca.value);
		actionFixture.setfeaturedElement(featToOntoFixture.getOracle().get(acao.elemento.id));
		return actionFixture;
	} else if (acao.isTypeOf(Designar)) {
		actionFixture = featToOntoFixture.getOracle().get(acao.id);
		actionFixture.setValue(acao.valor);
		actionFixture.setAttribute(featToOntoFixture.getOracle().get(acao.atributo.id));
		actionFixture.setValueType(acao.tipoValor.value);
		return actionFixture;
	} else {
		return null;
	}
}

operation preencherEvento(evento: Evento, factoryFixture, splFixture, featToOntoFixture) {
	var eventFixture = null;
	if (evento.isTypeOf(EventoLogico)) {
		eventFixture = featToOntoFixture.getOracle().get(evento.id);
		eventFixture.setLeftSideEvent(preencherEvento(evento.ladoEsquerdoEvento, factoryFixture, splFixture, featToOntoFixture));
		eventFixture.setRightSideEvent(preencherEvento(evento.ladoDireitoEvento, factoryFixture, splFixture, featToOntoFixture));
		eventFixture.setLogicalOperator(evento.operadorLogico.value);
		return eventFixture;
	} else if (evento.isTypeOf(EventoRelacional)) {
		eventFixture = featToOntoFixture.getOracle().get(evento.id);
		eventFixture.setContextVariable(featToOntoFixture.getOracle().get(evento.variavelDeContexto.id));
		eventFixture.setRelationalOperator(evento.operadorRelacional.value);
		eventFixture.setValue(evento.valor);
		return eventFixture;
	} else {
		return null;
	}
}

operation preencherRegraDeComposicao(regraDeComposicao: RegraDeComposicao, factoryFixture, splFixture, featToOntoFixture) {
	var compositionRuleFixture = null;
	var antecedentFixture = null;
	var consequentFixture = null;
	
	compositionRuleFixture = featToOntoFixture.getOracle().get(regraDeComposicao.id);
	antecedentFixture = preencherAntecedente(regraDeComposicao.antecedente, factoryFixture, splFixture, featToOntoFixture);
	consequentFixture = preencherAntecedente(regraDeComposicao.consequente, factoryFixture, splFixture, featToOntoFixture);
	compositionRuleFixture.setAntecedent(antecedentFixture);
	compositionRuleFixture.setConsequent(consequentFixture);
	
	return compositionRuleFixture;
}

//TODO preencher a coleção de expressões. Só adiciona elementos em regras.
operation preencherAntecedente(antecedente : Antecedente, factoryFixture, splFixture, featToOntoFixture) {
	var antecedentFixture = null;
	if (antecedente.isTypeOf(ExpressaoLogica)) {
		antecedentFixture = featToOntoFixture.getOracle().get(antecedente.id);
		antecedentFixture.setLeftSideComposition(preencherAntecedente(antecedente.ladoEsquerdoComposicao, factoryFixture, splFixture, featToOntoFixture));
		antecedentFixture.setRightSideComposition(preencherAntecedente(antecedente.ladoDireitoComposicao, factoryFixture, splFixture, featToOntoFixture));
		antecedentFixture.setLogicalOperator(antecedente.operadorLogico.value);
		return antecedentFixture;
	} else if (antecedente.isTypeOf(LiteralComposicao)) {
		antecedentFixture = featToOntoFixture.getOracle().get(antecedente.id);
		antecedentFixture.setPresence(antecedente.presenca.value);
		antecedentFixture.setFeaturedElement(featToOntoFixture.getOracle().get(antecedente.elemento.id));
		return antecedentFixture;
	} else if (antecedente.isTypeOf(ExpressaoRelacional)) {
		antecedentFixture = featToOntoFixture.getOracle().get(antecedente.id);
		antecedentFixture.setExpressionVariable(featToOntoFixture.getOracle().get(antecedente.variaveldaExpressao.id));
		antecedentFixture.setRelationalOperator(antecedente.operadorRelacional.value);
		antecedentFixture.setValue(antecedente.valor);
		return antecedentFixture;
	} else {
		return null;
	}
}

//#### Método que realiza a análise da ontologia gerada a partir do MMSC
operation ontoAnalyserFixture() {
	var rulesAnalyser := new Native('fixture.owl.reasoner.RulesAnalyser');
	rulesAnalyser.run();
}

//############## MÉTODOS AUXILIARES AOS MÉTODOS PRINCIPAIS DO PARSER #####################
operation criarExpressaoFixture(elemento: Expressao, factoryFixture) {
	if (elemento.isTypeOf(EventoLogico)) {
		return factoryFixture.createLogicalEvent(elemento.id, elemento.nome);
	} else if (elemento.isTypeOf(EventoRelacional)) {
		return factoryFixture.createRelationalEvent(elemento.id, elemento.nome);
	} else if (elemento.isTypeOf(AcaoLogico)) {
		return factoryFixture.createLogicalAction(elemento.id, elemento.nome);
	} else if (elemento.isTypeOf(LiteralAcao)) {
		return factoryFixture.createActionLiteral(elemento.id, elemento.nome);
	} else if (elemento.isTypeOf(Designar)) {
		return factoryFixture.createDesignate(elemento.id, elemento.nome);
	} else if (elemento.isTypeOf(ExpressaoLogica)) {
		return factoryFixture.createLogicalExpression(elemento.id, elemento.nome);
	} else if (elemento.isTypeOf(ExpressaoRelacional)) {
		return factoryFixture.createRelationalExpression(elemento.id, elemento.nome);
	} else if (elemento.isTypeOf(LiteralComposicao)) {
		return factoryFixture.createCompositionLiteral(elemento.id, elemento.nome);
	} else {
		'[ERROR] - Erro ao criar nova Expressao pela Fixture: expressao do modelo é inválida.'.println();
		'[ERRO DETAIL]: '.print();
		elemento.nome.print();
		"".println(); 
		return null;
	}
}


operation criarElementoFixture(elemento: Elemento, factoryFixture) {
	if (elemento.isTypeOf(Atributo)) {
		return factoryFixture.createAttribute(elemento.id, elemento.nome);
	} else if (elemento.isTypeOf(CaracteristicaMandatoria)) {
		return factoryFixture.createMandatoryFeature(elemento.id, elemento.nome);
	} else if (elemento.isTypeOf(CaracteristicaOpcional)) {
		return factoryFixture.createOptionalFeature(elemento.id, elemento.nome);
	} else if (elemento.isTypeOf(VariacaoDois)) {
		return factoryFixture.createVariationTwo(elemento.id, elemento.nome, elemento.cardinalidadeMaxima, elemento.cardinalidadeMinimaOr, elemento.cardinalidadeMaximaOr);
	} else if (elemento.isTypeOf(CaracteristicaAgrupada)) {
		return factoryFixture.createGroupedFeature(elemento.id, elemento.nome);
	} else if (elemento.isTypeOf(RaizDeContexto)) {
		return factoryFixture.createContextRoot(elemento.id, elemento.nome);
	} else if (elemento.isTypeOf(EntidadeDeContexto)) {
		return factoryFixture.createContextEntity(elemento.id, elemento.nome);
	} else if (elemento.isTypeOf(InformacaoDeContexto)) {
		return factoryFixture.createContextInfo(elemento.id, elemento.nome);
	} else {
		'[ERROR] - Erro ao criar novo Elemento pela Fixture: elemento do modelo é inválido.'.println();
		'[ERRO DETAIL]: '.print();
		elemento.nome.print();
		"".println(); 
		return null;
	}
		
}

operation criarRegraFixture(elemento: Regra, factoryFixture) {
	if (elemento.isTypeOf(RegraDeContexto)) {
		return factoryFixture.createContextRule(elemento.id, elemento.nome);
	} else if (elemento.isTypeOf(RegraDeComposicao)) {
		return factoryFixture.createCompositionRule(elemento.id, elemento.nome);
	} else {
		'[ERROR] - Erro ao criar nova Regra pela Fixture: regra do modelo é inválida.'.println();
		'[ERRO DETAIL]: '.print();
		elemento.nome.print();
		"".println(); 
		return null;
	}

}
